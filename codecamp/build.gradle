apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
//    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        applicationId "com.gdogaru.codecamp"
        versionCode 20
        versionName "4.5.3"
        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        upload {
            storeFile file('certificate/upload-keystore.jks')
            storePassword "C0decamp"
            keyAlias "upload"
            keyPassword "C0decamp"
        }
        release {
            storeFile file('certificate/codecamp2-release-key.keystore')
            storePassword "C0decamp"
            keyAlias "codecamp2"
            keyPassword "C0decamp"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.upload
            minifyEnabled false
            shrinkResources false
        }

        release {
            signingConfig signingConfigs.upload
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "stage"
    productFlavors {
        dev {
            dimension "stage"
            signingConfig signingConfigs.upload
            minSdkVersion 24
            applicationIdSuffix '.dev'
            versionNameSuffix "-dev"

            resValue "string", "app_name", "Codecamp-DEV"
        }
        prod {
            signingConfig signingConfigs.upload
            dimension "stage"
            resValue "string", "app_name", "Codecamp"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude '.readme'
    }

    lintOptions {
        abortOnError false
        warning 'MissingPrefix'
        warning 'InvalidPackage'
        warning 'RestrictedApi'
    }

}

android.applicationVariants.all {
    // Set this to false to disable Firebase Performance Monitoring at compile time
    FirebasePerformance {
        instrumentationEnabled true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.android.support:multidex'
    implementation 'com.android.support:support-v4'
    implementation 'com.android.support:appcompat-v7'
    implementation 'com.android.support:design'
    implementation "com.android.support:cardview-v7"


    implementation "com.google.firebase:firebase-perf:15.2.0"
    implementation "com.google.firebase:firebase-core:15.0.2"
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.2'

    implementation "com.google.android.gms:play-services-maps:$gmsVersion"
    implementation "com.google.android.gms:play-services-base:$gmsVersion"
    implementation "com.google.android.gms:play-services-gcm:$gmsVersion"

    implementation 'uk.co.chrisjenx:calligraphy'

    implementation "com.google.dagger:dagger"
    implementation "com.google.dagger:dagger-android"
    implementation "com.google.dagger:dagger-android-support"
    kapt 'com.google.dagger:dagger-android-processor'
    kapt "com.google.dagger:dagger-compiler"

    implementation 'com.evernote:android-job'

    implementation 'com.jakewharton:butterknife'
    kapt 'com.jakewharton:butterknife-compiler'

    implementation 'com.jakewharton.timber:timber'

    implementation 'com.squareup.okio:okio'
    implementation 'com.squareup.okhttp3:okhttp'

    implementation 'joda-time:joda-time'

    implementation 'com.github.bumptech.glide:glide'

    implementation 'com.google.code.gson:gson'
    implementation 'org.greenrobot:eventbus'
    implementation 'se.emilsjolander:stickylistheaders'

    implementation 'frankiesardo:icepick'
    kapt 'frankiesardo:icepick-processor'

}

apply plugin: 'com.google.gms.google-services'
